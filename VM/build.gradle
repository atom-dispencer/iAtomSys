plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
  id 'maven-publish'
}

group = 'uk.iatom.iatomsys'
// Version is generated by GitHub Actions

bootRun {
	// Required for Gradle to pass console input to Spring Shell
	standardInput = System.in
}

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springShellVersion', "3.2.2")
}

dependencies {
	implementation ('org.springframework.shell:spring-shell-starter') {
		exclude(group: 'org.springframework.shell', module: 'spring-shell-standard-commands')
	}
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.jline:jline-terminal-jansi:3.25.1'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.shell:spring-shell-dependencies:${springShellVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

publishing { 
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/atom-dispencer/iatomsys"
      credentials {

        // Intended to be run from Github Actions, so no local auth is setup
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }

  publications {
    gpr(MavenPublication) {
      from(components.java)
    }
  }
}
